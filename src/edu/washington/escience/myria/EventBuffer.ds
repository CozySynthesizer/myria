{{
package edu.washington.escience.myria;
}}

EventBuffer:

    type Event = {
        queryId : Long,
        subqueryID : Long,
        fragmentId : Int,
        opId : Int,
        startTime : Long,
        endTime : Long,
        numTuples : Long
    }

    state events : Bag<Event>

    invariant all [x.startTime <= x.endTime | x <- events];

    op add(e : Event)
        assume e.startTime <= e.endTime;
        events.add(e);

    query getAnalyticsInTimespan(
            q_id : Long,
            sq_id : Long,
            frag_id : Int,
            start : Long,
            end : Long)
        assume start <= end;

        [e | e <- events, (
            e.queryId == q_id and
            e.subqueryID == sq_id and
            e.fragmentId == frag_id and
            e.startTime < end and
            e.endTime >= start)]
